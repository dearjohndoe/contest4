{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref.
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Flag and the value may be of different lengths.
  When there are overlapping flags, only the first one
  from the overlap needs to be replaced (for example, if the flag is 101,
  value is 111, and string is 10101, then the result would be 11101, but if
  the string is 1010101, then the result would be 1110111).
  Every cell in the input linked list, except for the last one
  (the one that is the deepest in the tree), is guaranteed to be full
  (guaranteed to contain 1023 bits).
  The requirements on the output cell are more loose - only the concatenation of bits
  from the linked list needs to match that of the expected answer, but the structure
  itself may be different (for example, for the purposes of comparing the answer,
  a cell with bit 1 and a ref containing bits 10 is the same as a cell containing
  bit 11 and a ref with bit 0 - they both make up a bitstring 110).
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...


-}

int min(int x, int y) asm "MIN";
(int) ubitsize(int a) asm "UBITSIZE";

(int) tlen(tuple t) asm "TLEN";
forall X -> (tuple, ()) push_back(tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back(tuple t) asm "UNCONS";

() recv_internal() {
}

(builder, (builder)) write_bits(builder out, int v, int s) {
  int limit = 1023 - out.builder_bits();
  if (s < limit) {
    out~store_uint(v, s);

    return (out, (begin_cell()));
  }

  out~store_uint(v >> s - limit, limit);

  builder next_cell = begin_cell();
  if (s > limit) {
    int mask = (1 << s - limit) - 1;
    next_cell~store_uint(v & mask, s - limit);
  }

  return (out, (next_cell));
}

int preload(slice sc, int read) inline {
  int left_bits = sc.slice_bits();

  ;; switch to next ref
  if (left_bits == 0) {
    sc = sc~load_ref().begin_parse();
    left_bits = sc.slice_bits();
  }

  int read_bits = min(left_bits, read);
  int bits = sc~load_uint(read_bits);
  if (left_bits < read) {
    ;; no cells anymore
    if (sc.slice_refs_empty?()) {
      return bits;
    }

    ;; load tail
    sc = sc~load_ref().begin_parse();
    int tail_len = read - read_bits;
    int tail = sc~load_uint(min(tail_len, sc.slice_bits()));

    bits = (bits << tail_len) + tail;
  }

  return bits;
}

(slice, (int)) load(slice sc, int read) {
  int left_bits = sc.slice_bits();

  ;; switch to next ref
  if (left_bits == 0) {
    sc = sc~load_ref().begin_parse();
    left_bits = sc.slice_bits();
  }

  ;; load from current
  int read_bits = min(left_bits, read);
  int bits = sc~load_uint(read_bits);
  if (left_bits < read) {
    ;; no cells anymore
    if (sc.slice_refs_empty?()) {
      return (sc, (bits));
    }

    ;; load tail
    sc = sc~load_ref().begin_parse();
    int tail_len = read - read_bits;
    int tail = sc~load(tail_len);

    bits = (bits << tail_len) + tail;
  }

  return (sc, (bits));
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  int flen = ubitsize(flag);
  int vlen = ubitsize(value);

  tuple out_stack = empty_tuple();
  var out = begin_cell();
  slice sc = linked_list.begin_parse();

  while (~ sc.slice_empty?()) {
    int bits = preload(sc, flen);

    builder new_out = null();
    if (bits == flag) {
      sc~load(flen);
      new_out = out~write_bits(value, vlen);
    } else {
      bits = sc~load(1);
      new_out = out~write_bits(bits, 1);
    }

    if (new_out.builder_bits()) {
      out_stack~push_back(out);
      out = new_out;
    }
  }

  while (out_stack.tlen() > 0) {
    out = out_stack~pop_back().store_ref(out.end_cell());
  }

  return out.end_cell();
}

