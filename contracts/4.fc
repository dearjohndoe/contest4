{-
  TASK 4 - Caesar Cipher
  Implement a Caesar cipher encryption and decryption functions.
  The input is a string (https://docs.ton.org/develop/smart-contracts/guidelines/internal-messages#simple-message-with-comment)
  where the text is encrypted in a cell (after 32bit 00000000 prefix flag), the algorithm rotates the characters and the last ASCII
  character should go to the beginning. Return new text encoded in cell.
-}

() recv_internal() {
}

int crypt_char(int s, int c) {
  if ((c >= 65) & (c <= 90)) {
    return 65 + (c - 65 + s) % 26;
  }
  if ((c >= 97) & (c <= 122)) {
    return 97 + (c - 97 + s) % 26;
  }

  return c;
}

cell do_ceasar(int shift, int first_symbol, cell text) {
  var encrypted = begin_cell();
  slice sc = text.begin_parse();

  if (first_symbol == -1) {
    sc~load_uint(32);
    encrypted~store_uint(0, 32);
  }

  ;; do base encrypt
  while (sc.slice_bits() >= 8) {
    encrypted~store_uint(crypt_char(shift, sc~load_uint(8)), 8);
  }

  ;; check tail
  if (~ sc.slice_refs_empty?()) {
    cell tail = do_ceasar(shift, 0, sc~load_ref());
    encrypted = store_ref(encrypted, tail);
  }

  return encrypted.end_cell();
}

;; testable
(cell) caesar_cipher_encrypt(int shift, cell text) method_id {
  return do_ceasar(shift % 26, -1, text);
}

;; testable
(cell) caesar_cipher_decrypt(int shift, cell text) method_id {
  return do_ceasar(shift * -1 % 26, -1, text);
}
