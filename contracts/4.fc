{-
  TASK 4 - Caesar Cipher
  Implement a Caesar cipher encryption and decryption functions.
  The input is a string (https://docs.ton.org/develop/smart-contracts/guidelines/internal-messages#simple-message-with-comment)
  where the text is encrypted in a cell (after 32bit 00000000 prefix flag), the algorithm rotates the characters and the last ASCII
  character should go to the beginning. Return new text encoded in cell.
-}

global int UpperShift;
global int LowerShift;

() recv_internal() {
}

int crypt_char(int c) {
  if ((c >= 65) & (c <= 90)) {
    return 65 + (c - UpperShift) % 26;
  }
  if ((c >= 97) & (c <= 122)) {
    return 97 + (c - LowerShift) % 26;
  }

  return c;
}

cell do_ceasar(int first_symbol, cell text) {
  var encrypted = begin_cell();
  slice sc = text.begin_parse();

  if (first_symbol == -1) {
    encrypted~store_uint(sc~load_uint(32), 32);
  }

  ;; do base encrypt
  while (~ sc.slice_data_empty?()) {
    encrypted~store_uint(crypt_char(sc~load_uint(8)), 8);
  }

  ;; check tail
  if (~ sc.slice_refs_empty?()) {
    encrypted = store_ref(encrypted, do_ceasar(0, sc~load_ref()));
  }

  return encrypted.end_cell();
}

;; testable
(cell) caesar_cipher_encrypt(int shift, cell text) method_id {
  UpperShift = 65 - shift % 26;
  LowerShift = 97 - shift % 26;
  return do_ceasar(-1, text);
}

;; testable
(cell) caesar_cipher_decrypt(int shift, cell text) method_id {
  UpperShift = 65 + shift % 26;
  LowerShift = 97 + shift % 26;
  return do_ceasar(-1, text);
}
